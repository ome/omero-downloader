apply plugin: 'application'
apply plugin: 'java'

mainClassName = 'ome.formats.importer.cli.CommandLineImporter'

def javaOpts = [
    '-Dlog4j.configuration=log4j-cli.properties',
    '-Xmx512M'
]

version '5.1.9'

repositories {
    mavenCentral()
    maven {
        url 'http://artifacts.openmicroscopy.org/artifactory/maven/'
    }
}

dependencies {
    compile(group: 'ome', name: 'formats-gpl', version: '5.1.1'){
    }
    compile(group: 'org.springframework.ldap', name: 'spring-ldap', version: '1.3.0.RELEASE', classifier: 'all'){
        exclude group: 'org.testng', module: 'testng'
    }
    compile(group: 'omero', name: 'blitz', version: '5.1.1-ice35-b43') {
        exclude group: 'ome', module: 'bio-formats'
        exclude group: 'org.springframework.ldap', module: 'spring-ldap'
        exclude group: 'org.testng', module: 'testng'
        exclude group: 'hsqldb', module: 'hsqldb'
    }
}

startScripts {
    doLast {
        def script = new File(getOutputDir(), applicationName)
        String content = script.text
        script.text = content.replaceFirst(
            'DEFAULT_JVM_OPTS=""',
            "DEFAULT_JVM_OPTS=\"${javaOpts.join(' ')}\"")
    }
}

run {
    args Eval.me("['-f', '.']")
}
